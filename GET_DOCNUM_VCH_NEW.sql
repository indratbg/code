create or replace FUNCTION GET_DOCNUM_VCH(
    pd_tgl  IN DATE,
    ps_rvpv IN VARCHAR2)
  RETURN VARCHAR2
IS
  vs_rvpv_num T_PAYRECH.PAYREC_NUM%TYPE;
  vs_doccd VARCHAR2(10) := NULL;
  v_mmyy T_PAYRECH.PAYREC_NUM%TYPE;
  vcounter     NUMBER(7) := 0;
  V_ERROR_CODE NUMBER(5);
  V_ERROR_MSG  VARCHAR2(200);
  V_SEQ_NAME   VARCHAR2(30);
  V_CNT        NUMBER(1);
  V_STR        VARCHAR2(200);
  PRAGMA autonomous_transaction;
  -- [IN] 07 MAR 2017 DITAMBAH UNION KE T_JVCHH DAN INBOX GL JOURNAL KARENA MENGGUNAKAN SEQUENCE YANG SAMA DENGAN GL JOURNAL
BEGIN
  -- Format : mmyyRV0000001  for Receipt Voucher Entry
  -- Format : mmyyPV0000001  for Payment Voucher Entry
  -- Format : mmyyRD0000001  for Direct Receipt Voucher Entry
  -- Format : mmyyPD0000001  for Direct Payment Voucher Entry
  V_SEQ_NAME :='SEQ_JUR_'||TO_CHAR(pd_tgl,'MMYY');
  vs_doccd   := trim(TO_CHAR(pd_tgl,'mmyy')||ps_rvpv);
  v_mmyy     := TO_CHAR(pd_tgl,'mmyy');
  
  BEGIN
    SELECT COUNT(1) INTO V_CNT FROM ALL_SEQUENCES WHERE SEQUENCE_NAME=V_SEQ_NAME;
  EXCEPTION
  WHEN OTHERS THEN
    RAISE_APPLICATION_ERROR(-20100,'CHECK SEQUENCE '||V_SEQ_NAME||'IN ALL_SEQUENCES'||SQLERRM);
  END;
  
IF V_CNT =0 THEN

  BEGIN
    SELECT NVL(MAX(TO_NUMBER(SUBSTR(PAYREC_NUM,8,7))),0)+1
    INTO vcounter
    FROM
      (
        SELECT payrec_num FROM T_PAYRECH a WHERE PAYREC_NUM LIKE v_mmyy||'%'
        UNION
        SELECT field_value
        FROM T_MANY_DETAIL a
        JOIN T_MANY_HEADER b
        ON a.UPDATE_DATE    = b.UPDATE_DATE
        AND a.UPDATE_SEQ    = b.UPDATE_SEQ
        WHERE b.UPDATE_DATE>=TRUNC(SYSDATE)
        AND TABLE_NAME      = 'T_PAYRECH'
        AND FIELD_NAME      = 'PAYREC_NUM'
        AND FIELD_VALUE LIKE v_mmyy||'%'
        AND APPROVED_STATUS = 'E'
        UNION
        SELECT JVCH_NUM JVCH_NUM
        FROM T_JVCHH
        WHERE JVCH_NUM LIKE v_mmyy||'%'
        AND SUBSTR(JVCH_NUM,8,3)<>'MFE'
        and SUBSTR(JVCH_NUM,8,3)<>'INT'
        UNION
        SELECT field_value JVCH_NUM
        FROM T_MANY_DETAIL a
        JOIN T_MANY_HEADER b
        ON a.UPDATE_DATE     = b.UPDATE_DATE
        AND a.UPDATE_SEQ     = b.UPDATE_SEQ
        WHERE b.UPDATE_DATE >=TRUNC(SYSDATE)
        AND TABLE_NAME       = 'T_JVCHH'
        AND FIELD_NAME       = 'JVCH_NUM'
        AND FIELD_VALUE LIKE v_mmyy||'%'
        AND APPROVED_STATUS = 'E'
      );
  EXCEPTION
  WHEN OTHERS THEN
    RAISE_APPLICATION_ERROR(-20200,'GET MAX COUNTER '||SQLERRM);
  END;
  
  BEGIN
    V_STR :='CREATE SEQUENCE '||V_SEQ_NAME||' MINVALUE 0 MAXVALUE 9999999 INCREMENT BY 1 START WITH '||vcounter;
    EXECUTE IMMEDIATE V_STR;
  EXCEPTION
  WHEN OTHERS THEN
    RAISE_APPLICATION_ERROR(-20300,'CREATE SEQUENCE '||V_SEQ_NAME||SQLERRM);
  END;
  
END IF;

BEGIN
  V_STR :='SELECT '||V_SEQ_NAME||'.NEXTVAL FROM DUAL';
  EXECUTE IMMEDIATE V_STR INTO vcounter;
EXCEPTION
WHEN OTHERS THEN
  RAISE_APPLICATION_ERROR(-20400,'GET SEQ USING EXECUTE IMMEDIATE '||SQLERRM);
END;

vs_rvpv_num := vs_doccd||'A'||TO_CHAR(NVL(vcounter,0),'fm0000000');

RETURN vs_rvpv_num;

END GET_DOCNUM_VCH;